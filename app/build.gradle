/*
 * Exif Notes
 * Copyright (C) 2022  Tommi Hirvonen
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <https://www.gnu.org/licenses/>.
 */

import org.apache.tools.ant.taskdefs.condition.Os
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-parcelize'
apply plugin: 'com.jaredsburrows.license'

android {
    signingConfigs {
        config {
            def propsFile = rootProject.file('keystore.properties')
            if (propsFile.exists()) {
                def props = new Properties()
                props.load(new FileInputStream(propsFile))
                if (Os.isFamily(Os.FAMILY_MAC)) {
                    storeFile file(props['storeFileMac'])
                }
                if (Os.isFamily(Os.FAMILY_WINDOWS)) {
                    storeFile file(props['storeFileWin'])
                }
                storePassword = props['storePassword']
                keyAlias = props['keyAlias']
                keyPassword = props['keyPassword']
            }
        }
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    compileSdkVersion 32
    buildToolsVersion '31.0.0'
    defaultConfig {
        applicationId 'com.tommihirvonen.exifnotes'
        minSdkVersion 21
        targetSdkVersion 32
        versionCode 37
        versionName "1.17.2"
        // Enabling this allows us to use resources when PNGs are generated during build-time
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner = 'androidx.test.runner.AndroidJUnitRunner'
    }

    buildTypes {
        def propsFile = rootProject.file('googlemapsapi.properties')
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
            if (propsFile.exists()) {
                def props = new Properties()
                props.load(new FileInputStream(propsFile))
                resValue "string", "google_maps_key", props['googleMapsKey']
            } else {
                resValue "string", "google_maps_key", ''
            }
        }
        debug {
            signingConfig signingConfigs.config
            if (propsFile.exists()) {
                def props = new Properties()
                props.load(new FileInputStream(propsFile))
                resValue "string", "google_maps_key", props['googleMapsKey']
            } else {
                resValue "string", "google_maps_key", ''
            }
        }
    }

    buildFeatures {
        viewBinding true
    }

    compileOptions {
        targetCompatibility = 1.8
        sourceCompatibility = 1.8
    }

    return void // Used to mute a warning about not all execution paths returning a value
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.5.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'com.google.android.gms:play-services-maps:18.1.0'
    implementation 'com.google.android.gms:play-services-location:20.0.0'
    implementation 'androidx.percentlayout:percentlayout:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.exifinterface:exifinterface:1.3.3'
    implementation "androidx.core:core-ktx:1.8.0"
    implementation 'androidx.preference:preference-ktx:1.2.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.3'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.5.1'
    implementation 'org.apache.commons:commons-text:1.9'
    implementation 'androidx.fragment:fragment-ktx:1.5.2'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.4.0'
    androidTestImplementation 'androidx.test:core-ktx:1.4.0'
}
repositories {
    mavenCentral()
}
